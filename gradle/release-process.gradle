apply plugin: 'release'
apply plugin: 'console'
apply plugin: 'github'

version = project('core').version

def bumpVersion(String v) {
    v.replaceAll(
        /(\d+)\.(\d+)\.*/,
        { m -> "${m[1]}.${(m[2] as int) + 1}-SNAPSHOT" }
    )
}

github {
    repo = 'git@github.com:evanchooly/afton.git'

    wiki {
        from(project(':core').javadoc.outputs.files)
        into { "javadoc/$project.release.version" }
    }

    release {
        tag = { "r$project.release.version" }
        name = { "$project.release.version" }
    }
}

release {

    doFirst {
        if (project.git.status().uncommitted) {
            throw new GradleException('You have uncommitted changes.')
        }
    }

    version = project.version - '-SNAPSHOT'
    tag = { "r$project.release.version" }
    commitMessage = { "Release $project.release.version" }

    update {
        file project.file('build.gradle')
        projects allprojects
    }

    next {
        version = bumpVersion(project.release.version)
        commitMessage = { "Bumping version to $project.release.next.version" }
    }

    dependsOn subprojects.uploadArchives
    dependsOn publishWiki
    dependsOn draftGhRelease
}